import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id "org.ajoberstar.grgit" version "3.1.1"
    id "org.jetbrains.kotlin.jvm" version "1.3.41"
    id "org.jetbrains.kotlin.kapt" version "1.3.41"
    id "com.github.ben-manes.versions" version "0.21.0"
    id "com.github.johnrengelman.shadow" version "5.1.0"
}
allprojects {
    def repoSlugs = ["stachu540", "ALICE"]
    def repoSlug = "${repoSlugs[0]}/${repoSlugs[1]}"
    def projectUrl = "https://${repoSlugs[0]}.github.io/${repoSlugs[1]}"
    def githubUrl = "https://github.com/$repoSlug"

    ext.bintrayUser = System.getenv("BINTRAY_USER") ?: findProperty("bintray.user").toString()
    ext.bintrayApiKey = System.getenv("BINTRAY_API_KEY") ?: findProperty("bintray.api_key").toString()
    ext.githubToken = System.getenv("GITHUB_TOKEN") ?: findProject("github.token").toString()
    ext.isSnapshot = (rootProject.version as String).endsWith("-SNAPSHOT")
    ext.projectUrl = [
            base  : projectUrl,
            github: githubUrl,
            slug  : repoSlug
    ]
    ext.tags = ["alice", "bot", "mutli-purpose", "modularity", "ktor"]

    repositories {
        maven { url = "https://dl.bintray.com/stachu540/Java" }
        jcenter()
    }
}

subprojects {
    if (name.startsWith("alice")) {
        apply plugin: "kotlin"
        apply plugin: "kotlin-kapt"

        dependencies {
            compile "org.slf4j:slf4j-api:1.7.26"

            compile "org.jetbrains.kotlin:kotlin-reflect"
            compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

            compile platform("io.jooby:jooby-bom:2.0.2")

            testCompile "org.jetbrains.kotlin:kotlin-test-junit5"
            testCompile "org.spekframework.spek2:spek-dsl-jvm:2.0.5"
            testCompile "org.spekframework.spek2:spek-runner-junit5:2.0.5"
            testCompile "org.jetbrains.kotlin:kotlin-test-junit5"
        }

        tasks.withType(KotlinCompile) {
            incremental = true
            kotlinOptions.jvmTarget = "1.8"
        }

        shadowJar {
            classifier = "fatjar"
        }

        test {
            useJUnitPlatform {
                includeEngines "spek2"
            }
        }
    }
}

wrapper {
    gradleVersion = "5.5.1"
    distributionType = Wrapper.DistributionType.ALL
}